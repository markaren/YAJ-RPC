
class PublishPluginExtension {
    String description = "Yet Another JSON RPC (YAJ-RPC) is a JSON RPC 2.0 implementation for JVM languages written in Kotlin"
}

class PublishPlugin implements Plugin<Project>{

    protected Jar sourcesJar(Project project) {
        Jar task = project.tasks.create("sourcesJar", Jar.class)
        task.from project.sourceSets.main.allSource
        task.classifier = 'sources'
        return task
    }

    protected Jar javadocJar(Project project) {
        Jar task = project.tasks.create("javadocJar", Jar.class)
        task.from project.javadoc.destinationDir
        task.classifier = 'javadoc'
        return task
    }

    void apply(Project project) {
        if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {

            project.apply(plugin: MavenPublishPlugin)
            project.apply(plugin: SigningPlugin)

            def snapshot = true
            project.group = 'info.laht'
            project.version = '0.9.1'
            if (snapshot) {
                project.version += '-SNAPSHOT'
            }

            println("maven version for ${project.archivesBaseName} is: ${project.version}")

            def extension = project.extensions.create('pomDescription', PublishPluginExtension)

            project.publishing {
                publications {
                    mavenJava(MavenPublication) {

                        from project.components.java
                        artifact sourcesJar(project)
                        artifact javadocJar(project)

                        pom {
                            name = project.archivesBaseName
                            description = extension.description
                            url = 'https://markaren.github.io/YAJ-RPC/'

                            scm {
                                connection = 'scm:git:git://github.com/markaren/YAJ-RPC.git'
                                developerConnection = 'scm:git:ssh://github.com/markaren/YAJ-RPC.git'
                                url = 'https://github.com/markaren/YAJ-RPC/tree/master'
                            }

                            licenses {
                                license {
                                    name = 'The MIT License'
                                    url = 'https://opensource.org/licenses/mit-license.php'
                                }
                            }

                            developers {
                                developer {
                                    id = 'laht'
                                    name = 'Lars Ivar Hatledal'
                                    email = 'larsivarhatledal@gmail.no'
                                }
                            }
                        }
                    }
                }
                repositories {
                    maven {
                        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                        url project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                            username = project.ossrhUsername
                            password = project.ossrhPassword
                        }
                    }
                }
            }

            project.signing {
                sign project.publishing.publications.mavenJava
            }

        }
    }

}

apply plugin: PublishPlugin